utils = FFI clone("utils", """
def tobin(x):
    return bin(int(x))

def tobool(x):
    return bool(x)

def tochr(x):
    return chr(int(x))

def tohex(x):
    return hex(int(x))
""")

abs = block(x,
    Number clone setValue(x :__abs__)
)

all = block(xs,
    xs foreach(x,
        (x bool) ifFalse(return False)
    )
    True
)

any = block(xs,
    xs foreach(x,
        (x bool) ifTrue(return True)
    )
    False
)

bin = block(x, utils tobin(x))

bool = block(x, utils tobool(x))

callable = block(x,
    (x has("__call__")) ifTrue(
        return True
    ) ifFalse(
        return (x type == "Block")
    )
)

chr = block(x, utils tochr(x))

dict = block(
    d = Dict clone
    call message args foreach(arg,
        arg eval(d)
    )
    d
)

eval = block(code, context,
    Parser parse(code) eval(context ifNone(Root))
)

exit = block(status,
    status ifNone(
        System exit(0)
    )
    System exit(status)
)

forward = block(name, self get(name))

hex = block(x, utils tohex(x))

from = import = Importer get("import")

map = block(f, xs,
    rs = List clone
    xs foreach(x,
        rs append(f(x))
    )
    rs
)

filter = block(f, xs,
    rs = List clone
    xs foreach(x,
        (f is None) ifTrue(
            not x is None ifTrue(
                rs append(x)
            )
        ) ifFalse(
            f(x) ifTrue(
                rs append(x)
            )
        )
    )
    rs
)

list = block(
    return (List clone(call message evalArgs(call sender)))
)

open = block(filename,
    return File clone open(filename)
)

print = block(*args, sep=" ", end="\n", file=System stdout,
    file write(sep join(args))
    file write(end)
    file flush
    None
)

range = block(Range clone(call message evalArgs(call sender)))

raise = Exception get("raise")

sum = block(xs,
    r = 0
    xs foreach(x,
        r += x
    )
    r
)

try = Exception get("try")

with = block(
    obj = call message arg(0, call sender)
    res = call message arg(1, call sender)
    obj close
    res
)

#
# Syntactic Sugar
#

set("()", Object get("evalArg"))
set(":", Object get("primitive"))
set("[]", get("list"))
set("{}", get("dict"))

set("?",
    block(
        name = call message args at(0) name
        call target has(name) ifTrue(
            call target get(name)
        ) ifFalse(
            None
        )
    )
)

from errors import *
